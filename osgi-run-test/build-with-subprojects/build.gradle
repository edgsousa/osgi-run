buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.dm.gradle:gradle-bundle-plugin:0.8.2'
    }
}

apply plugin: 'com.athaydes.osgi-run'

repositories {
    mavenLocal()
    mavenCentral()
}

project.buildDir = file 'dist'

subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: 'com.athaydes.osgi-run'

    // make a simple environment for each sub-project
    runOsgi {
        bundles += project
        copyManifestTo file( 'generated-sources/MANIFEST.MF' )
    }
}

dependencies {
    osgiRuntime osgi( group: 'org.apache.commons', name: 'commons-lang3', version: '3.3.2', startLevel: 2 )
}

runOsgi {
    // to use Equinox, run gradle -Pequinox
    if ( project.hasProperty( 'equinox' ) ) {
        configSettings = 'equinox'
        programArgs = '-console'
    }
    bundles += subprojects
    bundlesPath = 'jars'
    config += [ 'org.osgi.framework.storage.clean'       : 'onFirstInit',
                'org.osgi.framework.startlevel.beginning': 10 ]
}
