apply plugin: 'com.athaydes.osgi-run'

subprojects {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
}

def apiModules = [ 'hello-service' ]

/* Configuring all sub-projects here for convenience */

configure( subprojects.findAll { it.name == 'hello-service' } ) {
    // api bundles do not need to be ipojoized
    apply plugin: 'java'
    apply plugin: 'osgi'

    jar {
        manifest {
            instruction 'Export-Package', 'ipojo.example.hello'
        }
    }
}

configure( subprojects.findAll { !( it.name in apiModules ) } ) {
    apply plugin: 'com.athaydes.ipojo'

    ipojo {
        ignoreAnnotations = true
        failIfNoIPojoComponents = true
    }
    jar {
        manifest {
            instruction 'Export-Package', ''
        }
    }
    dependencies {
        compile project( ':osgi-run-test:ipojo-xml-example:hello-service' )
    }
}

runOsgi {
    bundles = [ subprojects ] + IPOJO_ALL_BUNDLES
}
